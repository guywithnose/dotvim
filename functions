" vim: filetype=vim
function! DockerBuild()
  let b:imageName=fnamemodify(bufname('%'), ':p:h:t')
  execute '!docker build -t ' . b:imageName . ' . && docker run -it ' . b:imageName . ' bash'
endfunction

function! PushCurrentBranch()
  let b:currentBranchName=fugitive#head()
  if b:currentBranchName != ''
    execute 'Gpush origin ' . b:currentBranchName
  else
    echo 'Not currently on a branch!'
  endif
endfunction

function! PushCurrentBranchForce()
  let b:currentBranchName=fugitive#head()
  if b:currentBranchName != ''
    execute 'Gpush -f origin ' . b:currentBranchName
  else
    echo 'Not currently on a branch!'
  endif
endfunction

function! SplitArray()
  let oldSearch = @/

  execute 's/\[\(.\)/\[\r\1/g | s/, /,\r/g | s/\(.\)\]/\1,\r\]/g | normal =a]'

  let @/ = oldSearch
endfunction

function! Stab()
  let l:tabstop = 1 * input('set tabstop = softtabstop = shiftwidth = ')
  if l:tabstop > 0
    let &l:softtabstop = l:tabstop
    let &l:tabstop = l:tabstop
    let &l:shiftwidth = l:tabstop
  endif
endfunction

function! VSetSearch()
  let temp = @s
  normal! gv"sy
  let @/ = '\V' . substitute(escape(@s, '/\'), '\n', '\\n', 'g')
  let @s = temp
endfunction

" Set the arglist by the files contained in the quickfix list
function! QuickfixArgs()
  let buffer_numbers = {}
  for quickfix_item in getqflist()
    let buffer_numbers[quickfix_item['bufnr']] = bufname(quickfix_item['bufnr'])
  endfor
  exec 'silent args ' join(map(values(buffer_numbers), 'fnameescape(v:val)'))
endfunction
