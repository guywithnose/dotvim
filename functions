" vim: filetype=vim

" Sets the tabstop, softtabstop, and shiftwidth to the same value
function! Stab()
  let l:tabstop = 1 * input('set tabstop = softtabstop = shiftwidth = ')
  if l:tabstop > 0
    let &l:softtabstop = l:tabstop
    let &l:tabstop = l:tabstop
    let &l:shiftwidth = l:tabstop
  endif
endfunction

" Set the arglist by the files contained in the quickfix list
function! QuickfixArgs()
  let buffer_numbers = {}
  for quickfix_item in getqflist()
    let buffer_numbers[quickfix_item['bufnr']] = bufname(quickfix_item['bufnr'])
  endfor
  exec 'silent args ' join(map(values(buffer_numbers), 'fnameescape(v:val)'))
endfunction

" Detect whitespace at the end of lines
function! CountTrailingWhitespaceLines()
  let searchCount = 0
  normal G$
  let flags = "w"
  while search('\s\+$', flags)
    normal j0
    let searchCount += 1
    let flags = "W"
  endwhile
  if searchCount > 0
    echo 'Found' searchCount 'lines with trailing whitespace'
  endif
endfunction

" Run a command without moving the cursor
function! Preserve(command)
  let _s=@/
  let l = line(".")
  let c = col(".")
  execute a:command
  let @/=_s
  call cursor(l, c)
endfunction
