" vim: filetype=vim
set tabline=%!MyTabLine()
function! MyTabLine()
    let s = ''
    let tabs = []
    for i in range(tabpagenr('$'))
      let thisTab = ''

      " set the tab page number (for mouse clicks)
      let thisTab .= '%' . (i + 1) . 'T'

      " the label is made by MyTabLabel()
      let thisTab .= ' %{MyTabLabel(' . (i + 1) . ', ' . &columns / tabpagenr('$') . ')} '

      call add(tabs, thisTab)
    endfor

    for i in range(tabpagenr('$'))
      if i + 1 == tabpagenr()
        let s .= '%#TabLineSel#'
      else
        let s .= '%#TabLine#'
      endif

      let s .= tabs[i]
    endfor

    return s
endfunction

function! MyTabLabel(n, maxwidth)
  let buflist = tabpagebuflist(a:n)
  let winnr = tabpagewinnr(a:n)
  let bn = bufname(buflist[winnr - 1])
  return a:n . ' ' . strpart(bn, strlen(bn) - a:maxwidth, a:maxwidth)
endfunction
